        ORG 0
        B MAIN

        ORG 0X18;ADRESA POTPROGRAMA ZA IRQ
IRQ     STMFD SP!, {R1, R2, R3, R4, R5, LR};SPREMANJE KONTEKSTA
        LDR R1, GPIO1
        LDR R2, GPIO2
        LDR R3, RTC
        LDR R4, COUNTER      
        
        
        ;POSTAVLJANJE STANJA SEMAFORA 1.SLUCAJ: ZABRANJEN PROLAZ AUTIMA (5.LEDICA)
        CMP R4, #0
        MOVEQ R5, #0B00100000
        STREQ R5, [R2];PODESI GPIO2
        BLEQ SHOW1

        ;POSTAVLJANJE STANJA SEMAFORA 2.SLUCAJ: ZABRANJEN PROLAZ AUTIMA(5.LEDICA)
        CMP R4, #1
        MOVEQ R5, #0B00100000
        STREQ R5, [R2];PODESI GPIO2
        BLEQ SHOW2
        
        ;POSTAVLJANJE STANJA SEMAFORA 3.SLUCAJ: PRIPREMA DOZVOLE AUTIMA (5. I 6. LEDICA)
        CMP R4, #2
        MOVEQ R5, #0B01100000
        STREQ R5, [R2];PODESI GPIO2

        ;POSTAVLJANJE STANJA SEMAFORA 4.SLUCAJ: SLOBODAN PROLAZ (7.LEDICA)
        CMP R4, #3
        MOVEQ R5, #0B10000000
        STREQ R5, [R2];PODESI GPIO2

        ;POSTAVLJANJE STANJA SEMAFORA 5.SLUCAJ: PRIPREMA ZABRANE PROLAZA (6.LEDICA)
        CMP R4, #4
        MOVEQ R5, #0B01000000
        STREQ R5, [R2];PODESI GPIO2

        ;POSTAVLJANJE STANJA SEMAFORA 6.SLUCAJ: ZABRANJEN PROLAZ (5.LEDICA)
        CMP R4, #5
        MOVEQ R5, #0B00100000
        STREQ R5, [R2];PODESI GPIO2

        ADD R4, R4, #1;POVECAVANJE BROJACA DA BI SE MOGAO USPOREDITI SA COUNTEROM
        CMP R4, #6;USPOREDI JE LI BROJAC NAPRAVIO CIKLUS SA SVIM STANJIMA
        MOVEQ R4, #0;AKO JE, RESETIRAJ GA
        
        STR R4, COUNTER

        MOV R5, #0
        STR R5, [R3, #12];POSTAVLJANJE BROJILA NA 0(LR)
        STR R5, [R3, #8];END OF INTERRUPT -> 0 KOD PISANJA

        LDMFD SP!, {R1, R2, R3, R4, R5, LR};OBNOVI KONTEKST
        SUBS PC, LR, #4;POVRATAK IZ PREKIDNOG PROGRAMA

SHOW1   STMFD SP!, {R0,LR};ISPIS HODAJ SPREMANJEM ZELJENOG ASCII ZNAKA U R0 I POZIVANJEM FUNKCIJE LCDWR
        MOV R0, #0X0D;CLEAR
        BL LCDWR
        MOV R0, #0X48;H
        BL LCDWR
        MOV R0, #0X4F;0
        BL LCDWR
        MOV R0, #0X44;D
        BL LCDWR
        MOV R0, #0X41;A
        BL LCDWR
        MOV R0, #0X4A;J
        BL LCDWR
        MOV R0, #0X0A;SET
        BL LCDWR

        LDMFD SP!, {R0, LR}
        MOV PC, LR
        

SHOW2   STMFD SP!, {R0, LR};ISPIS STANI SPREMANJEM ZELJENOG ASCII ZNAKA U R0 I POZIVANJEM FUNKCIJE LCDWR
        MOV R0, #0X0D;CLEAR
        BL LCDWR
        MOV R0, #0X53;S
        BL LCDWR
        MOV R0, #0X54;T
        BL LCDWR
        MOV R0, #0X41;A
        BL LCDWR
        MOV R0, #0X4E;N
        BL LCDWR
        MOV R0, #0X49;I
        BL LCDWR
        MOV R0, #0X0A;SET
        BL LCDWR

        LDMFD SP!, {R0, LR}
        MOV PC, LR

LCDWR   STMFD SP!, {R0,R1}

        LDR R1, GPIO1

        AND R0, R0, #0X7F;SPUSTI SIGNAL -> UCITAVANJE PODATKA TAKO DA JE 7 BIT SPUSTEN
        STRB R0,[R1, #4];AZURIRAJ GPIO1

        ORR R0, R0, #0X80;DIGNI SIGNAL -> POSTAVLJANJE 7 BITA U 1
        STRB R0, [R1, #4];AZURIRAJ GPIO1
        
        AND R0,R0, #0X7F;SPUSTI SIGNAL -> POSTAVLJANJE 7 BITA U 1
        STRB R0, [R1, #4];AZURIRAJ GPIO1

        LDMFD SP!, {R0,R1}
        MOV PC, LR



MAIN    MSR CPSR, #0B11010010 ; IRQ NACIN RADA -> 1 NA 7 BITU DA ONEMOGUCI PREKIDE *24 SLAJD
        MOV SP, #0X8000 ; INICIJALIZACIJA STOGA

        MSR CPSR, #0B01010011 ; 0 NA 7 BITU DA OMOGUCIM PREKIDE U SVC NACINU RADA
        MOV SP, #0X4000 ; INICIJALIZACIJA STOGA

        LDR R1, GPIO1; R1 CUVA ADRESU GPIO1
        LDR R2, GPIO2; R2 CUVA ADRESU GPIO2
        LDR R3, RTC;R3 CUVA ADRESU RTC

        ;INICIJALIZACIJA GPIO1 
        MOV R4, #0 
        STR R4, [R1, #12];-> U PB_DDR UPISUJEM 0 -> SMJER VRATA B JE IZLAZ -> SVI BITOVI VRATA B SU IZLAZI

        ;INICIJALIZACIJA GPIO 2
        MOV R4, #0B11100000
        STR R4, [R2, #8];-> U PA_DDR UPISUJEM 1 -> PRVA TRI BITA VRATA A SU IZLAZNA

        ;INICIJALIZACIJA RTC-A
        MOV R4, #5
        STR R4, [R3, #4]; ;UPISUJEM KONSTANTU BROJENJA U MR
        MOV R4, #0
        STR R4, [R3, #12];INICIJALIZIRAM BROJILO NA 0
        MOV R4, #1
        STR R4, [R3,#16];OMOGUCAVANJE PREKIDA UPISIVANJEM 1 U CR

        ;INICIJALIZACIJA POCETNOG STANJA SEMAFORA
        MOV R4, #0B00100000; 00100000 -> AKTIVNA PETA LEDICA -> CRVENO SVJETLO
        STR R4, [R2];SPREMAM POCETNU VRIJEDNOST U GPIO2

        ;ALTERNATIVNI NACIN ZA OMOGUCAVANJE PREKIDA
        ;MRS R4, CPSR
        ;BIC R4, R4, #0B10000000
        ;MSR CPSR, R4
        

   
LOOP    B LOOP

        
      
GPIO1   DW 0XFFFF0F00;ADRESA
GPIO2   DW 0XFFFF0B00;ADRESA
RTC     DW 0XFFFF0E00;ADRESA

COUNTER DW 1

;GPIO2
;NA XPA[5] CRVENI LED
;NA XPA[6] ZUTI LED
;NA XPA[7] ZELENI LED

;GPIO1
;RADIMO S LCD PA KORISTIMO ULAZ B(INICIJALNO SMJER (PB_DDR) JE IZLAZ, A TO NAM I TREBA
;AKO JE ADRESA GPIO-A NPR 100, ZA PB_DR MI TREBA 100 S POMAKOM ZA 4